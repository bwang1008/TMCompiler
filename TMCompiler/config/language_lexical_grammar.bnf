# reference from https://learn.microsoft.com/en-us/cpp/c-language/lexical-grammar?view=msvc-170

# <token> ::= <identifier> | <keyword> | <constant> | <punctuator> | <comment>

# make sure don't have
# - (keyword, keyword)
# - (keyword, identifier)
# - (keyword, constant)
# - (identifier, keyword)
# - (identifier, identifier)
# - (identifier, constant)
# - (constant, keyword)
# - (constant, identifier)
# - (constant, constant)
# - (whitespace, whitespace)
# <tokens> ::= <token>
	# | <token> <tokens>
	# | <token> <token-delimiter> <tokens>

<tokens> ::= <tokens-with-optional-whitespace-prefix>
		   | <tokens-with-optional-whitespace-prefix> <whitespace>

<tokens-with-optional-whitespace-prefix> ::= <tokens-no-whitespace-prefix>
										   | <token-delimiter> <tokens-no-whitespace-prefix>

<tokens-no-whitespace-prefix> ::= <keyword>
								| <keyword> <punctuator>
								| <keyword> <punctuator> <tokens-with-optional-whitespace-prefix>
								| <keyword> <token-delimiter>
								| <keyword> <token-delimiter> <tokens-no-whitespace-prefix>
								| <identifier>
								| <identifier> <punctuator>
								| <identifier> <punctuator> <tokens-with-optional-whitespace-prefix>
								| <identifier> <token-delimiter>
								| <identifier> <token-delimiter> <tokens-no-whitespace-prefix>
								| <constant>
								| <constant> <punctuator>
								| <constant> <punctuator> <tokens-with-optional-whitespace-prefix>
								| <constant> <token-delimiter>
								| <constant> <token-delimiter> <tokens-no-whitespace-prefix>
								| <punctuator>
								| <punctuator> <tokens-with-optional-whitespace-prefix>

<token> ::= <keyword>
		  | <identifier>
		  | <constant>
		  | <punctuator>

<token-delimiter> ::= <whitespace>

<keyword> ::= "v" "o" "i" "d"
			| "i" "n" "t"
			| "b" "o" "o" "l"
			| "f" "o" "r"
			| "w" "h" "i" "l" "e"
			| "i" "f"
			| "e" "l" "s" "e"
			| "b" "r" "e" "a" "k"
			| "c" "o" "n" "t" "i" "n" "u" "e"
			| "r" "e" "t" "u" "r" "n"

<identifier> ::= <identifier-nondigit>
			   | <identifier> <identifier-nondigit>
			   | <identifier> <digit>

<constant> ::= <integer-constant>
			 | <boolean-constant>

<integer-constant> ::= <digit>
					 | <nonzero-digit> <digits>

<boolean-constant> ::= "t" "r" "u" "e"
					 | "f" "a" "l" "s" "e"

<punctuator> ::= "[" | "]" | "(" | ")" | "{" | "}" | "*" | "+" | "-" | "!" | "/" | "%" | "<" | ">" | "^" | ";" | "=" | "," | "&" | "|"

# utility
<whitespace> ::= <whitespace-chars>
			   | <whitespace> <whitespace-chars>

<whitespace-chars> ::= " "
					 | "\n"
					 | "\t"

<identifier-nondigit> ::= <nondigit>

<nondigit> ::= "_" | "a" | "b" | "c" | "d" | "e" | "f" | "g" | "h" | "i" | "j" | "k" | "m" | "n" | "o" | "p" | "q" | "r" | "s" | "t" | "u" | "v" | "w" | "x" | "y" | "z" | "A" | "B" | "C" | "D" | "E" | "F" | "G" | "H" | "I" | "J" | "K" | "M" | "N" | "O" | "P" | "Q" | "R" | "S" | "T" | "U" | "V" | "W" | "X" | "Y" | "Z"

<nonzero-digit> ::= "1" | "2" | "3" | "4" | "5" | "6" | "7" | "8" | "9"

<digit> ::= "0"
		  | <nonzero-digit>

<digits> ::= <digit>
		   | <digit> <digits>

# regex ones
# <token-delimiter> ::= "\w+"
# <identifier> ::= "[a-zA-Z_][a-zA-Z0-9_]*"
